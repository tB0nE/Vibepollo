# linux specific compile definitions

if(FREEBSD)
    add_compile_definitions(SUNSHINE_PLATFORM="freebsd")
else()
    add_compile_definitions(SUNSHINE_PLATFORM="linux")
endif()

# AppImage
if(SUNSHINE_BUILD_STEAMOS)
    list(APPEND SUNSHINE_DEFINITIONS SUNSHINE_BUILD_STEAMOS=1)
endif()
if(${SUNSHINE_BUILD_APPIMAGE})
    # use relative assets path for AppImage
    string(REPLACE "${CMAKE_INSTALL_PREFIX}" ".${CMAKE_INSTALL_PREFIX}" SUNSHINE_ASSETS_DIR_DEF ${SUNSHINE_ASSETS_DIR})
endif()

# cuda
if(SUNSHINE_REQUIRE_CUDA_PASCAL AND NOT SUNSHINE_ENABLE_CUDA)
    message(FATAL_ERROR "Pascal-compatible release packages require SUNSHINE_ENABLE_CUDA=ON.")
endif()
set(CUDA_FOUND OFF)
if(${SUNSHINE_ENABLE_CUDA})
    include(CheckLanguage)
    check_language(CUDA)

    if(CMAKE_CUDA_COMPILER)
        set(CUDA_FOUND ON)
        enable_language(CUDA)

        include("${CMAKE_CURRENT_LIST_DIR}/cuda_architectures.cmake")
    elseif(CUDA_FAIL_ON_MISSING OR SUNSHINE_REQUIRE_CUDA_PASCAL)
        message(FATAL_ERROR
                "CUDA not found.
                If this is intentional, set '-DSUNSHINE_ENABLE_CUDA=OFF' or '-DCUDA_FAIL_ON_MISSING=OFF'"
        )
    endif()
endif()
if(CUDA_FOUND)
    include_directories(SYSTEM "${CMAKE_SOURCE_DIR}/third-party/nvfbc")
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/cuda.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/cuda.cu"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/cuda.cpp"
            "${CMAKE_SOURCE_DIR}/third-party/nvfbc/NvFBC.h")

    add_compile_definitions(SUNSHINE_BUILD_CUDA)
endif()

# libdrm is required for DRM (KMS), KWin ScreenCast and Wayland
if(${SUNSHINE_ENABLE_DRM} OR ${SUNSHINE_ENABLE_KWIN} OR ${SUNSHINE_ENABLE_WAYLAND})
    find_package(LIBDRM REQUIRED)
else()
    set(LIBDRM_FOUND OFF)
endif()
if(LIBDRM_FOUND)
    include_directories(SYSTEM ${LIBDRM_INCLUDE_DIRS})
    list(APPEND PLATFORM_LIBRARIES ${LIBDRM_LIBRARIES})
endif()

# The Linux entry point always sanitizes its process capabilities before it
# parses configuration or initializes logging.  Keep libcap available even in
# portable/non-DRM Linux builds; only the KMS implementation itself is gated by
# SUNSHINE_ENABLE_DRM.
if(CMAKE_SYSTEM_NAME STREQUAL "Linux" OR ${SUNSHINE_ENABLE_DRM})
    find_package(LIBCAP REQUIRED)
else()
    set(LIBCAP_FOUND OFF)
endif()
if(CMAKE_SYSTEM_NAME STREQUAL "Linux" AND LIBCAP_FOUND)
    include_directories(SYSTEM ${LIBCAP_INCLUDE_DIRS})
    list(APPEND PLATFORM_LIBRARIES ${LIBCAP_LIBRARIES})
endif()

# drm
if(${SUNSHINE_ENABLE_DRM} AND LIBDRM_FOUND AND LIBCAP_FOUND)
    add_compile_definitions(SUNSHINE_BUILD_DRM)
    if(NOT CMAKE_SYSTEM_NAME STREQUAL "Linux")
        include_directories(SYSTEM ${LIBCAP_INCLUDE_DIRS})
        list(APPEND PLATFORM_LIBRARIES ${LIBCAP_LIBRARIES})
    endif()
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/kmsgrab.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/kms_capture_client.cpp")
    list(APPEND SUNSHINE_DEFINITIONS EGL_NO_X11=1)
endif()

# evdev
include(dependencies/libevdev_Sunshine)

# vaapi
if(${SUNSHINE_ENABLE_VAAPI})
    find_package(Libva REQUIRED)
else()
    set(LIBVA_FOUND OFF)
endif()
if(LIBVA_FOUND)
    add_compile_definitions(SUNSHINE_BUILD_VAAPI)
    include_directories(SYSTEM ${LIBVA_INCLUDE_DIR})
    list(APPEND PLATFORM_LIBRARIES ${LIBVA_LIBRARIES} ${LIBVA_DRM_LIBRARIES})
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/vaapi.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/vaapi.cpp")
endif()

# vulkan video encoding (via FFmpeg)
if(${SUNSHINE_ENABLE_VULKAN})
    if(NOT SUNSHINE_SYSTEM_VULKAN_HEADERS)
        # use Vulkan headers from build-deps submodule (system headers may be too old, e.g. Ubuntu 22.04)
        set(VULKAN_HEADERS_DIR "${CMAKE_SOURCE_DIR}/third-party/build-deps/third-party/FFmpeg/Vulkan-Headers/include")
    else()
        find_package(VulkanHeaders REQUIRED)
        get_target_property(VULKAN_HEADERS_DIR Vulkan::Headers INTERFACE_INCLUDE_DIRECTORIES)
    endif()

    if(NOT EXISTS "${VULKAN_HEADERS_DIR}/vulkan/vulkan.h")
        message(FATAL_ERROR "Vulkan headers not found in build-deps submodule")
    endif()

    find_library(VULKAN_LIBRARY NAMES vulkan vulkan-1)
    if(NOT VULKAN_LIBRARY)
        message(FATAL_ERROR "libvulkan not found")
    endif()

    # prefer glslc, fall back to glslangValidator
    find_program(GLSLC_EXECUTABLE glslc)
    if(NOT GLSLC_EXECUTABLE)
        find_program(GLSLANG_EXECUTABLE glslangValidator)
    endif()
    if(NOT GLSLC_EXECUTABLE AND NOT GLSLANG_EXECUTABLE)
        message(FATAL_ERROR "Vulkan shader compiler not found (need glslc or glslangValidator)")
    endif()

    list(APPEND SUNSHINE_DEFINITIONS SUNSHINE_BUILD_VULKAN=1)
    include_directories(SYSTEM ${VULKAN_HEADERS_DIR})
    list(APPEND PLATFORM_LIBRARIES ${VULKAN_LIBRARY})
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/vulkan_encode.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/vulkan_encode.cpp")

    # compile GLSL -> SPIR-V -> C include at build time
    set(VULKAN_SHADER_DIR "${CMAKE_BINARY_DIR}/generated-src/shaders")
    set(VULKAN_SHADER_SOURCE "${SUNSHINE_SOURCE_ASSETS_DIR}/linux/assets/shaders/vulkan/rgb2yuv.comp")
    set(VULKAN_SHADER_SPV "${VULKAN_SHADER_DIR}/rgb2yuv.spv")
    set(VULKAN_SHADER_DATA "${VULKAN_SHADER_DIR}/rgb2yuv.spv.inc")

    file(MAKE_DIRECTORY "${VULKAN_SHADER_DIR}")

    if(GLSLC_EXECUTABLE)
        add_custom_command(
                OUTPUT "${VULKAN_SHADER_SPV}"
                COMMAND ${GLSLC_EXECUTABLE} -O "${VULKAN_SHADER_SOURCE}" -o "${VULKAN_SHADER_SPV}"
                DEPENDS "${VULKAN_SHADER_SOURCE}"
                COMMENT "Compiling Vulkan shader rgb2yuv.comp (glslc)"
                VERBATIM)
    else()
        add_custom_command(
                OUTPUT "${VULKAN_SHADER_SPV}"
                COMMAND ${GLSLANG_EXECUTABLE} -V -o "${VULKAN_SHADER_SPV}" "${VULKAN_SHADER_SOURCE}"
                DEPENDS "${VULKAN_SHADER_SOURCE}"
                COMMENT "Compiling Vulkan shader rgb2yuv.comp (glslangValidator)"
                VERBATIM)
    endif()

    add_custom_command(
            OUTPUT "${VULKAN_SHADER_DATA}"
            COMMAND ${CMAKE_COMMAND} -DSPV_FILE=${VULKAN_SHADER_SPV} -DOUT_FILE=${VULKAN_SHADER_DATA}
                -P "${CMAKE_SOURCE_DIR}/cmake/scripts/binary_to_c.cmake"
            DEPENDS "${VULKAN_SHADER_SPV}"
            COMMENT "Generating C include from rgb2yuv.spv"
            VERBATIM)

    add_custom_target(vulkan_shaders
            DEPENDS "${VULKAN_SHADER_DATA}"
            COMMENT "Vulkan shader compilation")
    set(SUNSHINE_TARGET_DEPENDENCIES ${SUNSHINE_TARGET_DEPENDENCIES} vulkan_shaders)
endif()

# wayland
if(${SUNSHINE_ENABLE_WAYLAND})
    find_package(Wayland REQUIRED)
else()
    set(WAYLAND_FOUND OFF)
endif()
if(WAYLAND_FOUND)
    add_compile_definitions(SUNSHINE_BUILD_WAYLAND)

    if(NOT SUNSHINE_SYSTEM_WAYLAND_PROTOCOLS)
        set(WAYLAND_PROTOCOLS_DIR "${CMAKE_SOURCE_DIR}/third-party/wayland-protocols")
    else()
        pkg_get_variable(WAYLAND_PROTOCOLS_DIR wayland-protocols pkgdatadir)
        pkg_check_modules(WAYLAND_PROTOCOLS wayland-protocols REQUIRED)
    endif()

    GEN_WAYLAND("${WAYLAND_PROTOCOLS_DIR}" "unstable/xdg-output" xdg-output-unstable-v1)
    GEN_WAYLAND("${WAYLAND_PROTOCOLS_DIR}" "unstable/linux-dmabuf" linux-dmabuf-unstable-v1)
    GEN_WAYLAND("${CMAKE_SOURCE_DIR}/third-party/wlr-protocols" "unstable" wlr-screencopy-unstable-v1)

    include_directories(
            SYSTEM
            ${WAYLAND_INCLUDE_DIRS}
            ${CMAKE_BINARY_DIR}/generated-src
    )

    list(APPEND PLATFORM_LIBRARIES ${WAYLAND_LIBRARIES} gbm)
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/wlgrab.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/wayland.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/wayland.cpp")
endif()

# x11
if(${SUNSHINE_ENABLE_X11})
    find_package(X11 REQUIRED)
else()
    set(X11_FOUND OFF)
endif()
if(X11_FOUND)
    add_compile_definitions(SUNSHINE_BUILD_X11)
    include_directories(SYSTEM ${X11_INCLUDE_DIR})
    list(APPEND PLATFORM_LIBRARIES ${X11_LIBRARIES})
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/x11grab.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/x11grab.cpp")
endif()

# NvFBC -> Vulkan PyroWave. Uses only the CUDA driver (dlopen), so it works with or without
# the full CUDA backend. With it, PyroWave sessions use this display and the other codecs keep
# the CUDA NvFBC -> NVENC path.
if(SUNSHINE_ENABLE_NVFBC_VK AND SUNSHINE_ENABLE_PYROWAVE)
    add_compile_definitions(SUNSHINE_BUILD_NVFBC_VK)
    include_directories(SYSTEM
            "${CMAKE_SOURCE_DIR}/third-party/nvfbc"
            "${CMAKE_SOURCE_DIR}/third-party/nv-codec-headers/include")
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/nvfbc_vk.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/nvfbc_vk.cpp")
endif()

# GIO
pkg_check_modules(GIO gio-2.0 gio-unix-2.0 REQUIRED)
if(GIO_FOUND)
    include_directories(SYSTEM ${GIO_INCLUDE_DIRS})
    list(APPEND PLATFORM_LIBRARIES ${GIO_LIBRARIES})
endif()

# Pipewire
if(${SUNSHINE_ENABLE_KWIN} OR ${SUNSHINE_ENABLE_PORTAL} OR SUNSHINE_ENABLE_GAMESCOPE)
    pkg_check_modules(PIPEWIRE libpipewire-0.3 REQUIRED)
else()
    set(PIPEWIRE_FOUND OFF)
endif()
if(PIPEWIRE_FOUND)
    include_directories(SYSTEM ${PIPEWIRE_INCLUDE_DIRS})
    list(APPEND PLATFORM_LIBRARIES ${PIPEWIRE_LIBRARIES})
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/pipewire.cpp")
endif()

# XDG portal
if(SUNSHINE_ENABLE_GAMESCOPE)
    if(NOT WAYLAND_FOUND OR NOT PIPEWIRE_FOUND)
        message(FATAL_ERROR "Gamescope capture requires Wayland and PipeWire")
    endif()
    add_compile_definitions(SUNSHINE_BUILD_GAMESCOPE)
    GEN_WAYLAND("${CMAKE_SOURCE_DIR}/src/platform/linux/protocols" "" gamescope-pipewire)
    GEN_WAYLAND("${CMAKE_SOURCE_DIR}/src/platform/linux/protocols" "" vibeshine-capture-v1)
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/gamescope_session.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/gamescope_display_backend.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/gamescopegrab.cpp")
endif()

set(PORTAL_FOUND OFF)
if(PIPEWIRE_FOUND AND GIO_FOUND AND ${SUNSHINE_ENABLE_PORTAL})
    set(PORTAL_FOUND ON)
    add_compile_definitions(SUNSHINE_BUILD_PORTAL)
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/portalgrab.cpp")
endif()

# KWin ScreenCast (direct Wayland protocol, bypasses portal)
set(KWIN_FOUND OFF)
if(PIPEWIRE_FOUND AND WAYLAND_FOUND AND ${SUNSHINE_ENABLE_KWIN})
    set(KWIN_FOUND ON)
    add_compile_definitions(SUNSHINE_BUILD_KWIN)
    GEN_WAYLAND("${CMAKE_SOURCE_DIR}/third-party/plasma-wayland-protocols/src/protocols" "" kde-output-order-v1)
    GEN_WAYLAND("${CMAKE_SOURCE_DIR}/third-party/plasma-wayland-protocols/src/protocols" "" zkde-screencast-unstable-v1)
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/kwingrab.cpp")
elseif(${SUNSHINE_ENABLE_KWIN} AND NOT WAYLAND_FOUND)
    message(FATAL_ERROR "SUNSHINE_ENABLE_KWIN requires SUNSHINE_ENABLE_WAYLAND — KWin capture disabled")
endif()

if(NOT ${CUDA_FOUND}
        AND NOT (${LIBDRM_FOUND} AND ${LIBCAP_FOUND})
        AND NOT ${LIBVA_FOUND}
        AND NOT ${KWIN_FOUND}
        AND NOT ${PORTAL_FOUND}
        AND NOT ${WAYLAND_FOUND}
        AND NOT ${X11_FOUND})
    message(FATAL_ERROR "Couldn't find either cuda, (libdrm and libcap), libva, kwin, pipewire, portal, wayland or x11")
endif()

# tray icon
if(${SUNSHINE_ENABLE_TRAY})
    pkg_check_modules(APPINDICATOR ayatana-appindicator3-0.1)
    if(APPINDICATOR_FOUND)
        list(APPEND SUNSHINE_DEFINITIONS TRAY_AYATANA_APPINDICATOR=1)
    else()
        pkg_check_modules(APPINDICATOR appindicator3-0.1)
        if(APPINDICATOR_FOUND)
            list(APPEND SUNSHINE_DEFINITIONS TRAY_LEGACY_APPINDICATOR=1)
        endif ()
    endif()
    pkg_check_modules(LIBNOTIFY libnotify)
    if(NOT APPINDICATOR_FOUND OR NOT LIBNOTIFY_FOUND)
        message(STATUS "APPINDICATOR_FOUND: ${APPINDICATOR_FOUND}")
        message(STATUS "LIBNOTIFY_FOUND: ${LIBNOTIFY_FOUND}")
        message(FATAL_ERROR "Couldn't find either appindicator or libnotify")
    else()
        include_directories(SYSTEM ${APPINDICATOR_INCLUDE_DIRS} ${LIBNOTIFY_INCLUDE_DIRS})
        link_directories(${APPINDICATOR_LIBRARY_DIRS} ${LIBNOTIFY_LIBRARY_DIRS})

        list(APPEND PLATFORM_TARGET_FILES "${CMAKE_SOURCE_DIR}/third-party/tray/src/tray_linux.c")
        list(APPEND SUNSHINE_EXTERNAL_LIBRARIES ${APPINDICATOR_LIBRARIES} ${LIBNOTIFY_LIBRARIES})
    endif()

    # flatpak icons must be prefixed with the app id or they will not be included in the flatpak
    if(${SUNSHINE_BUILD_FLATPAK})
        set(SUNSHINE_TRAY_PREFIX "${PROJECT_FQDN}")
    else()
        set(SUNSHINE_TRAY_PREFIX "apollo")
    endif()
    list(APPEND SUNSHINE_DEFINITIONS SUNSHINE_TRAY_PREFIX="${SUNSHINE_TRAY_PREFIX}")
else()
    set(SUNSHINE_TRAY 0)
    message(STATUS "Tray icon disabled")
endif()

# These need to be set before adding the inputtino subdirectory in order for them to be picked up
set(LIBEVDEV_CUSTOM_INCLUDE_DIR "${EVDEV_INCLUDE_DIR}")
set(LIBEVDEV_CUSTOM_LIBRARY "${EVDEV_LIBRARY}")
if(FREEBSD)
    set(USE_UHID OFF)
endif()

add_subdirectory("${CMAKE_SOURCE_DIR}/third-party/inputtino")
list(APPEND SUNSHINE_EXTERNAL_LIBRARIES inputtino::libinputtino)
file(GLOB_RECURSE INPUTTINO_SOURCES
        ${CMAKE_SOURCE_DIR}/src/platform/linux/input/inputtino*.h
        ${CMAKE_SOURCE_DIR}/src/platform/linux/input/inputtino*.cpp)
list(APPEND PLATFORM_TARGET_FILES ${INPUTTINO_SOURCES})

# build libevdev before the libinputtino target
if(EXTERNAL_PROJECT_LIBEVDEV_USED)
    add_dependencies(libinputtino libevdev)
endif()

# AppImage and Flatpak
if (${SUNSHINE_BUILD_APPIMAGE})
    list(APPEND SUNSHINE_DEFINITIONS SUNSHINE_BUILD_APPIMAGE=1)
endif ()
if (${SUNSHINE_BUILD_FLATPAK})
    list(APPEND SUNSHINE_DEFINITIONS SUNSHINE_BUILD_FLATPAK=1)
endif ()

list(APPEND PLATFORM_TARGET_FILES
        "${CMAKE_SOURCE_DIR}/src/provider_scan_protocol.h"
        "${CMAKE_SOURCE_DIR}/src/provider_scan_protocol.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/publish.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/graphics.h"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/graphics.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/misc.h"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/misc.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/mangohud_policy.h"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/secure_open.h"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/secure_open.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/host_stats.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/audio.cpp")
if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    list(APPEND PLATFORM_TARGET_FILES
            "${CMAKE_SOURCE_DIR}/src/platform/linux/routed_link.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/routed_link.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/display_power.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/frame_limiter.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/private_display.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/display_backend.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/private_display_mode_client.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/private_display_mode_client.cpp"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/private_display_restore_policy.h"
            "${CMAKE_SOURCE_DIR}/src/platform/linux/private_display.cpp")
endif()

list(APPEND PLATFORM_LIBRARIES
        dl
        pulse
        pulse-simple)

list(APPEND SUNSHINE_EXTERNAL_LIBRARIES glad)

if(SUNSHINE_ENABLE_PYROWAVE AND CMAKE_SYSTEM_NAME STREQUAL "Linux")
    find_package(LIBDRM REQUIRED)
    find_program(PYROWAVE_GLSLC glslc)
    if(PYROWAVE_GLSLC)
        set(PYROWAVE_SHADER_COMPILER ${PYROWAVE_GLSLC})
        set(PYROWAVE_SHADER_FLAGS -O)
    else()
        find_program(PYROWAVE_GLSLANG glslangValidator REQUIRED)
        set(PYROWAVE_SHADER_COMPILER ${PYROWAVE_GLSLANG})
        set(PYROWAVE_SHADER_FLAGS -V)
    endif()
    set(PYROWAVE_SHADER_DIR "${CMAKE_BINARY_DIR}/generated-src/shaders")
    file(MAKE_DIRECTORY "${PYROWAVE_SHADER_DIR}")
    set(PYROWAVE_SHADER "${SUNSHINE_SOURCE_ASSETS_DIR}/linux/assets/shaders/vulkan/pyrowave.comp")
    add_custom_command(OUTPUT "${PYROWAVE_SHADER_DIR}/pyrowave.spv.inc"
        COMMAND ${PYROWAVE_SHADER_COMPILER} ${PYROWAVE_SHADER_FLAGS} "${PYROWAVE_SHADER}" -o "${PYROWAVE_SHADER_DIR}/pyrowave.spv"
        COMMAND ${CMAKE_COMMAND} -DSPV_FILE=${PYROWAVE_SHADER_DIR}/pyrowave.spv
            -DOUT_FILE=${PYROWAVE_SHADER_DIR}/pyrowave.spv.inc
            -P "${CMAKE_SOURCE_DIR}/cmake/scripts/binary_to_c.cmake"
        DEPENDS "${PYROWAVE_SHADER}" "${CMAKE_SOURCE_DIR}/cmake/scripts/binary_to_c.cmake"
        VERBATIM)
    add_library(pyrowave-linux STATIC
        "${CMAKE_SOURCE_DIR}/src/platform/linux/pyrowave_core.cpp"
        "${CMAKE_SOURCE_DIR}/src/platform/linux/cuda_shared.cpp"
        "${PYROWAVE_SHADER_DIR}/pyrowave.spv.inc")
    target_compile_features(pyrowave-linux PRIVATE cxx_std_17)
    target_compile_options(pyrowave-linux PRIVATE -fvisibility=hidden)
    target_include_directories(pyrowave-linux PRIVATE "${CMAKE_BINARY_DIR}/generated-src" ${LIBDRM_INCLUDE_DIRS})
    target_include_directories(pyrowave-linux SYSTEM PRIVATE "${CMAKE_SOURCE_DIR}/third-party/nv-codec-headers/include")
    target_link_libraries(pyrowave-linux PRIVATE pyrowave granite-vulkan)
    list(APPEND PLATFORM_LIBRARIES pyrowave-linux)
    list(APPEND PLATFORM_TARGET_FILES "${CMAKE_SOURCE_DIR}/src/platform/linux/pyrowave_encode.cpp")
endif()
